plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "androidx.navigation.safeargs.kotlin"
}

android {
    compileSdkVersion builds.compile_sdk
    defaultConfig {
        applicationId "wee.digital.sample"
        minSdkVersion builds.min_sdk
        targetSdkVersion builds.target_sdk
        versionCode builds.version_code
        versionName builds.version_name
        flavorDimensions "default"
        consumerProguardFiles "consumer-rules.pro"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    signingConfigs {
        release {
        }
    }
    buildTypes {
        all {
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        release {
            signingConfig signingConfigs.release
            debuggable true
            minifyEnabled true
            shrinkResources true
        }
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
    }
    productFlavors {
        all {
        }
        dev {
            resValue "string", "app_name", "Sample Dev"
            applicationIdSuffix ".dev"
        }
        qc {
            resValue "string", "app_name", "Sample QC"
            applicationIdSuffix ".qc"
        }
        prod {
            resValue "string", "app_name", "Sample"
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ["libs"]
        }
    }
    aaptOptions {
        noCompress "tflite"
    }
    buildFeatures {
        viewBinding = true
    }
}

dependencies {

    implementation fileTree(include: ["*.jar", "*aar"], dir: "libs")

    // utils lib
    implementation project(":library")

    // widgets lib
    implementation project(":widget")

    // kotlin
    implementation deps.kotlin.stdlib

    // concurrency pattern
    implementation deps.kotlin.coroutines_core
    implementation deps.kotlin.coroutines_android

    // android
    implementation deps.android.corektx
    implementation deps.android.multidex
    implementation deps.android.appcompat
    implementation deps.android.activity
    implementation deps.android.fragment
    implementation deps.android.constraintlayout
    implementation deps.android.material
    implementation deps.android.viewpager
    implementation deps.android.recyclerview
    implementation deps.android.cardview
    implementation deps.android.swiperefreshlayout
    implementation deps.android.browser
    implementation deps.android.annotation
    implementation deps.android.vectordrawable
    implementation deps.android.security

    // fragment navigation
    implementation deps.navigation.fragment
    implementation deps.navigation.ui

    // paging recycler view adapter
    implementation deps.paging.runtime

    // android lifecycle aware
    implementation deps.lifecycle.viewmodel
    implementation deps.lifecycle.livedata
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt deps.lifecycle.compiler

    // sq-lite database orm
    implementation deps.room.runtime
    annotationProcessor deps.room.compiler
    kapt deps.room.compiler

    // json convert
    implementation deps.google.gson

    // firebase sdk
    implementation platform(deps.firebase.bom)
    implementation deps.firebase.auth
    implementation deps.firebase.firestore
    implementation deps.firebase.storage

    // custom view
    implementation deps.bottom_navigation
    implementation deps.image_cropper
    implementation deps.rounded_image_view

    //unit test
    testImplementation deps.kotlin.test
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mockito:mockito-core:3.11.2"
    testImplementation "org.mockito:mockito-inline:3.11.2"
    testImplementation "org.mockito.kotlin:mockito-kotlin:3.2.0"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation deps.navigation.test
    androidTestImplementation "androidx.test:core:1.4.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    debugImplementation "androidx.fragment:fragment-testing:1.3.5"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0"
}

apply plugin: "com.google.gms.google-services"
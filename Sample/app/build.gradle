plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "androidx.navigation.safeargs.kotlin"
}

android {
    compileSdkVersion builds.compile_sdk
    defaultConfig {
        applicationId "wee.digital.sample"
        minSdkVersion builds.min_sdk
        targetSdkVersion builds.target_sdk
        versionCode builds.version_code
        versionName builds.version_name
        flavorDimensions "default"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    signingConfigs {
        release {
        }
    }
    buildTypes {
        all {
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        release {
            signingConfig signingConfigs.release
            debuggable true
            minifyEnabled true
            shrinkResources true
        }
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
    }
    productFlavors {
        all {flavor ->
            task("${flavor.name}GoogleServices", type: Copy) {
                description = 'Switches to google-services.json depending on flavor'
                from "src/${flavor.name}"
                include "google-services.json"
                into "."
            }
        }
        dev {
            resValue "string", "app_name", "Sample Dev"
            applicationIdSuffix ".dev"
        }
        prod {
            resValue "string", "app_name", "Sample"
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ["libs"]
        }
    }
    buildFeatures {
        viewBinding = true
    }
    androidResources {
        noCompress 'tflite'
    }
}

dependencies {

    implementation fileTree(include: ["*.jar", "*aar"], dir: "libs")

    // utils lib
    implementation project(":library")

    // widgets lib
    implementation project(":widget")

    // widgets lib
    implementation project(":camera")

    // kotlin
    implementation deps.kotlin.stdlib

    // concurrency pattern
    implementation deps.kotlin.coroutines_core
    implementation deps.kotlin.coroutines_android

    // android
    implementation deps.android.corektx
    implementation deps.android.multidex
    implementation deps.android.appcompat
    implementation deps.android.activity
    implementation deps.android.fragment
    implementation deps.android.constraintlayout
    implementation deps.android.material
    implementation deps.android.viewpager
    implementation deps.android.recyclerview
    implementation deps.android.cardview
    implementation deps.android.swiperefreshlayout
    implementation deps.android.browser
    implementation deps.android.annotation
    implementation deps.android.vectordrawable
    implementation deps.android.security
    implementation deps.android.legacy_v4

    // fragment navigation
    implementation deps.navigation.fragment
    implementation deps.navigation.ui

    // paging recycler view adapter
    implementation deps.paging.runtime

    // android lifecycle aware
    implementation deps.lifecycle.viewmodel
    implementation deps.lifecycle.livedata
    kapt deps.lifecycle.compiler

    // sq-lite database orm
    implementation deps.room.runtime
    annotationProcessor deps.room.compiler
    kapt deps.room.compiler

    // json convert
    implementation deps.google.gson

    // firebase sdk
    implementation platform(deps.firebase.bom)
    implementation deps.firebase.analytics
    implementation deps.firebase.auth
    implementation deps.firebase.firestore
    implementation deps.firebase.storage
    implementation deps.firebase.messaging

    // dimension resources
    implementation deps.dimens.sdp
    implementation deps.dimens.ssp

    // restful client
    implementation deps.square.retrofit
    implementation deps.square.okhttp
    implementation deps.square.okhttp_log
    implementation deps.square.gson

    implementation "com.google.mlkit:camera:16.0.0-beta2"

    // custom view
    implementation deps.rounded_image_view

    // MediaPipe Face Detection Solution.
    implementation 'com.google.mediapipe:solution-core:latest.release'
    implementation 'com.google.mediapipe:facedetection:latest.release'
    implementation 'com.google.mediapipe:facemesh:latest.release'
}

afterEvaluate {
    android.productFlavors.all {
        flavor ->
            tasks."generate${flavor.name.capitalize()}DebugResources".dependsOn "${flavor.name}GoogleServices"
            tasks."generate${flavor.name.capitalize()}ReleaseResources".dependsOn "${flavor.name}GoogleServices"
    }
}

apply plugin: "com.google.gms.google-services"